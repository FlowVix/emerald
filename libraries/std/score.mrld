


let {
    Score,
    CompType,
    RuntimeCondition,
} = import? "types.mrld";

export let Score = Score;

let id_counter = 0;

impl Score {

    new: (default: number) => {
        command("scoreboard players set mrld_" + string(id_counter) + " mrld_store " + string(floor(default)));
        return Score::{
            id: (id_counter += 1) - 1,
        }
    },
    str: (self) => "mrld_" + string(self.id) + " mrld_store",
    clone: (self) => {
        let c = Score::new(0);
        c.set(self);
        return c;
    },
    print: (self) => {
        command(
            "tellraw @a {\"score\":{\"name\":\"" +
            "mrld_" + string(self.id) +
            "\",\"objective\":\"mrld_store\"}}"
        );
    },

    add: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            if other < 0 {
                self.subtract(-other);
            } else {
                command("scoreboard players add " + self.str() + " " + string(other));
            }
        } else {
            command("scoreboard players operation " + self.str() + " += " + other.str());
        }
    },
    subtract: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            if other < 0 {
                self.add(-other);
            } else {
                command("scoreboard players remove " + self.str() + " " + string(other));
            }
        } else {
            command("scoreboard players operation " + self.str() + " -= " + other.str());
        }
    },
    multiply: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            let str = (if (other < 0) "n_int_" + string(-other) else "int_" + string(other)) + " mrld_store";
            command("scoreboard players set " + str + " " + string(other));
            command("scoreboard players operation " + self.str() + " *= " + str);
        } else {
            command("scoreboard players operation " + self.str() + " *= " + other.str());
        }
    },
    divide: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            let str = (if (other < 0) "n_int_" + string(-other) else "int_" + string(other)) + " mrld_store";
            command("scoreboard players set " + str + " " + string(other));
            command("scoreboard players operation " + self.str() + " /= " + str);
        } else {
            command("scoreboard players operation " + self.str() + " /= " + other.str());
        }
    },
    modulo: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            let str = (if (other < 0) "n_int_" + string(-other) else "int_" + string(other)) + " mrld_store";
            command("scoreboard players set " + str + " " + string(other));
            command("scoreboard players operation " + self.str() + " %= " + str);
        } else {
            command("scoreboard players operation " + self.str() + " %= " + other.str());
        }
    },
    set: (self, other: number | Score) => {
        if other is number {
            let other = floor(other);
            command("scoreboard players set " + self.str() + " " + string(other));
        } else {
            command("scoreboard players operation " + self.str() + " = " + other.str());
        }
    },

    
    _plus_eq_: (self, v: number | Score) => {
        self.add(v);
    },
    _minus_eq_: (self, v: number | Score) => {
        self.subtract(v);
    },
    _mult_eq_: (self, v: number | Score) => {
        self.multiply(v);
    },
    _div_eq_: (self, v: number | Score) => {
        self.divide(v);
    },
    _mod_eq_: (self, v: number | Score) => {
        self.modulo(v);
    },
    _assign_: (self, v: number | Score) => {
        self.set(v);
    },

    _plus_: (self, v: number | Score) => {
        let c = self.clone();
        c.add(v);
        return c;
    },
    _minus_: (self, v: number | Score) => {
        let c = self.clone();
        c.subtract(v);
        return c;
    },
    _mult_: (self, v: number | Score) => {
        let c = self.clone();
        c.multiply(v);
        return c;
    },
    _div_: (self, v: number | Score) => {
        let c = self.clone();
        c.divide(v);
        return c;
    },
    _mod_: (self, v: number | Score) => {
        let c = self.clone();
        c.modulo(v);
        return c;
    },

    _eq_: (self, v: number | Score) => {
        return RuntimeCondition:Comparison {
            left: self,
            comp: CompType:Equal,
            right: v,
        }
    },
    _greater_: (self, v: number | Score) => {
        return RuntimeCondition:Comparison {
            left: self,
            comp: CompType:Greater,
            right: v,
        }
    },
    _greater_eq_: (self, v: number | Score) => {
        return RuntimeCondition:Comparison {
            left: self,
            comp: CompType:GreaterEq,
            right: v,
        }
    },
    _lesser_: (self, v: number | Score) => {
        return RuntimeCondition:Comparison {
            left: self,
            comp: CompType:Lesser,
            right: v,
        }
    },
    _lesser_eq_: (self, v: number | Score) => {
        return RuntimeCondition:Comparison {
            left: self,
            comp: CompType:LesserEq,
            right: v,
        }
    },
}






