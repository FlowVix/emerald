

let {
    MCInt,
    MCFloat,
    CompType,
    RuntimeCondition,
} = import? "types.mrld";

export let RuntimeCondition = RuntimeCondition;

impl RuntimeCondition {
    then: (self, f: mc_func) => {

        match self {
            case RuntimeCondition:Comparison { left, comp, right } -> {
                match left {
                    MCInt -> {
                        let cond = "";
                        match right {
                            number -> {
                                let whole = floor(right);

                                if right == whole {
                                    cond = match comp {
                                        case CompType:Equal -> "matches " + string(whole),
                                        case CompType:Greater -> "matches " + string(whole+1) + "..",
                                        case CompType:GreaterEq -> "matches " + string(whole) + "..",
                                        case CompType:Lesser -> "matches .." + string(whole-1),
                                        case CompType:LesserEq -> "matches .." + string(whole),
                                    }
                                } else {
                                    cond = match comp {
                                        case CompType:Equal -> {return;},
                                        case CompType:Greater -> "matches " + string(whole+1) + "..",
                                        case CompType:GreaterEq -> "matches " + string(whole + 1) + "..",
                                        case CompType:Lesser -> "matches .." + string(whole),
                                        case CompType:LesserEq -> "matches .." + string(whole),
                                    }
                                }

                                command_if("score " + left.str() + " " + cond, f);
                            },
                            MCInt -> {
                                cond = match comp {
                                    case CompType:Equal -> "= " + right.str(),
                                    case CompType:Greater -> "> " + right.str(),
                                    case CompType:GreaterEq -> ">= " + right.str(),
                                    case CompType:Lesser -> "< " + right.str(),
                                    case CompType:LesserEq -> "<= " + right.str(),
                                }
                                command_if("score " + left.str() + " " + cond, f);
                            },
                            MCFloat -> {
                                let diff = right - left;
                                if match comp {
                                    case CompType:Equal -> diff.is_0(),
                                    case CompType:Greater -> diff.is_negative(),
                                    case CompType:GreaterEq -> (diff.is_negative() || diff.is_0()),
                                    case CompType:Lesser -> diff.is_over_0(),
                                    case CompType:LesserEq -> diff.is_positive(),
                                } {f!;}
                            },
                        }
                    },
                    MCFloat -> {
                        let diff = left - right;
                        
                        if match comp {
                            case CompType:Equal -> diff.is_0(),
                            case CompType:Greater -> diff.is_over_0(),
                            case CompType:GreaterEq -> diff.is_positive(),
                            case CompType:Lesser -> diff.is_negative(),
                            case CompType:LesserEq -> diff.is_negative() || diff.is_0(),
                        } {f!;}
                    }
                }
            },
            case RuntimeCondition:And (left, right) -> {
                if left {
                    if right {f!;}
                }
            },
            case RuntimeCondition:Or (left, right) -> {
                let cond_check = MCInt::new(0);
                let add = !{
                    cond_check += 1;
                }
                if left {add!;}
                if right {add!;}
                if cond_check > 0 {f!;}
            },
            case RuntimeCondition:IsBlock { pos, id } -> {
                command_if("block " + string(pos.x) + " " + string(pos.y) + " " + string(pos.z) + " " + id, f);
            },
        }
    },
    then_else: (self, f: mc_func, otherwise: mc_func) => {
        let is_true = MCInt::new(0);
        self.then(!{is_true += 1;});

        (is_true == 1).then(f);
        (is_true == 0).then(otherwise);
    },

    _and_: (self, other: RuntimeCondition) => {
        return RuntimeCondition:And (self, other);
    },
    _or_: (self, other: RuntimeCondition) => {
        return RuntimeCondition:Or (self, other);
    },

    _if_: (self, if_code, else_code) => {
        match else_code {
            case #(else_code) -> {
                self.then_else(!{
                    if_code();
                }, !{
                    else_code();
                });
            },
            case # -> {
                self.then(!{
                    if_code();
                });
            }
        }
    },

    _while_: (self, code, cond) => {
        self.then(!{
            code();
            cond().then($);
        });
    },


}